apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 401001
        versionName "4.1.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        def flavorName = variant.flavorName
        def buildType = variant.buildType.name
        variant.assemble << {
            copy {
                from("${project.buildDir}/intermediates/classes/${flavorName}/${buildType}")
                into("${project.buildDir}/outputs/classes")
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v13:21.0.+'
    compile 'droidkit:processor:1.0.+'
}

task("echoProperties") {
    println(project.buildNumber)
    project.properties.each {
        println(it)
    }
}

apply plugin: 'sonar-runner'

sonarRunner {
    sonarProperties {
        property 'sonar.projectKey', 'droidkit'
        property 'sonar.projectName', 'DroidKit'
        property 'sonar.projectVersion', "${android.defaultConfig.versionName}"
        property 'sonar.sources', 'src/main/java'
        property 'sonar.binaries', 'build/outputs/classes'

        property 'sonar.host.url', "${project.properties['sonar.host.url']}"
        property 'sonar.login', "${project.properties['sonar.login']}"
        property 'sonar.password', "${project.properties['sonar.password']}"
        property 'sonar.jdbc.url', "${project.properties['sonar.jdbc.url']}"
        property 'sonar.jdbc.driverClassName', "${project.properties['sonar.jdbc.driverClassName']}"
        property 'sonar.jdbc.username', "${project.properties['sonar.jdbc.username']}"
        property 'sonar.jdbc.password', "${project.properties['sonar.jdbc.password']}"
    }
}