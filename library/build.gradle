apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode rootProject.buildNumber
        versionName rootProject.versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        def flavorName = variant.flavorName
        def buildType = variant.buildType.name
        variant.assemble << {
            copy {
                from("${project.buildDir}/intermediates/classes/${flavorName}/${buildType}")
                into("${project.buildDir}/outputs/classes")
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v13:21.0.+'
    compile 'com.squareup:javapoet:1.0.+'
    compile files("${project.rootDir}/processor/build/libs/processor-${project.versionName}.jar")
}

task buildProcessor(type: GradleBuild) {
    dir = file('../processor')
    tasks = ['assemble']
}

tasks.preBuild {
    dependsOn buildProcessor
}

apply plugin: 'sonar-runner'

sonarRunner {
    sonarProperties {
        property 'sonar.projectKey', 'droidkit'
        property 'sonar.projectName', 'DroidKit'
        property 'sonar.projectVersion', "${android.defaultConfig.versionName}"
        property 'sonar.sources', 'src/main/java'
        property 'sonar.binaries', 'build/outputs/classes'

        property 'sonar.host.url', "${project.properties['sonar.host.url']}"
        property 'sonar.login', "${project.properties['sonar.login']}"
        property 'sonar.password', "${project.properties['sonar.password']}"
        property 'sonar.jdbc.url', "${project.properties['sonar.jdbc.url']}"
        property 'sonar.jdbc.username', "${project.properties['sonar.jdbc.username']}"
        property 'sonar.jdbc.password', "${project.properties['sonar.jdbc.password']}"
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                /*repository(url: "https://api.bintray.com/maven/dvp-troy/droidkit/droidkit") {
                    authentication(userName: bintrayUser, password: bintrayKey)
                }*/
                repository(url: "file://${project.rootDir}/../maven")
                pom.project {
                    issueManagement {
                        system 'github'
                        url 'https://github.com/dev-troy/droidkit/issues'
                    }
                    scm {
                        url 'scm:https://github.com/dev-troy/droidkit'
                        connection 'scm:git@github.com:dev-troy/droidkit.git'
                        developerConnection 'scm:git@github.com:dev-troy/droidkit.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'

                        }
                    }
                    developers {
                        developer {
                            name 'Daniel Serdyukov'
                            email 'dvp.troy@gmail.com'
                            url 'https://github.com/dev-troy'
                            roles {
                                role 'admin'
                                role 'developer'
                            }
                            timezone '+4'
                        }
                    }
                }
                pom.version = android?.defaultConfig?.versionName
                pom.groupId = "droidkit"
                pom.artifactId = 'droidkit'
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidSourcesJar(type: Jar) {
        from android.sourceSets.main.java.sourceFiles
        classifier = 'sources'
    }

    artifacts {
        archives androidSourcesJar
    }
}