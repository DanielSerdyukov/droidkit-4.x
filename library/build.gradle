apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode rootProject.buildNumber
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        def flavorName = variant.flavorName
        def buildType = variant.buildType.name
        variant.assemble << {
            copy {
                from("${project.buildDir}/intermediates/classes/${flavorName}/${buildType}")
                into("${project.buildDir}/outputs/classes")
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v13:21.0.+'
    compile 'com.google.guava:guava:18.0' // FIXME: remove, when javapoet will be added to jcenter
    compile files("${project.rootDir}/processor/build/libs/processor-${project.versionName}.jar")
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.+'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.+'
}

task buildProcessor(type: GradleBuild) {
    dir = file('../processor')
    tasks = ['assemble']
}

tasks.preBuild {
    dependsOn buildProcessor
}

if (project.hasProperty("sonar.host.url")) {
    apply from: 'sonar.gradle'
}

if (project.hasProperty("bintrayUser")) {
    apply from: 'bintray.gradle'
}